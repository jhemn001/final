//===- arm64_symbolization.dl -------------------------------*- datalog -*-===//
//
//  Copyright (C) 2019-2023 GrammaTech, Inc.
//
//  This code is licensed under the GNU Affero General Public License
//  as published by the Free Software Foundation, either version 3 of
//  the License, or (at your option) any later version. See the
//  LICENSE.txt file in the project root for license terms or visit
//  https://www.gnu.org/licenses/agpl.txt.
//
//  This program is distributed in the hope that it will be useful,
//  but WITHOUT ANY WARRANTY; without even the implied warranty of
//  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
//  GNU Affero General Public License for more details.
//
//  This project is sponsored by the Office of Naval Research, One Liberty
//  Center, 875 N. Randolph Street, Arlington, VA 22203 under contract #
//  N68335-17-C-0700.  The content of the information does not necessarily
//  reflect the position or policy of the Government and no official
//  endorsement should be inferred.
//
//===----------------------------------------------------------------------===//
/**
ARM64 architecture-specific rules for symbolization
*/

avoid_symbols("$x").

/**
Compute an immediate load performed across two consecutive instructions
*/
.decl split_load(ea:address, nextea:address, dest:address, type:symbol)

/** E.g.,
adrp x0, L1
add x0, x0, :lo12:L1
*/
split_load(EA, NextEA, as(Base + Offset,address), "ADD") :-
    // ADRP/ADR <Register> <Immediate>
    // e.g. adrp x0, BaseOp
    instruction_get_operation(EA,ADROperation),
    substr(ADROperation,0,3) = "ADR",
    instruction_get_op(EA, 1, BaseOp),
    op_immediate(BaseOp, Base),
    instruction_get_op(EA, 2, RegOp),
    op_regdirect_contains_reg(RegOp,Reg),

    // NOTE: Can't use def_used due to cyclic negation issue.
    //       This is a heuristic to look at next one or two instruction(s)
    //       to find ADD instruction.
    (
        next(EA, NextEA)
        ;
        next(EA, NextEA0), next(NextEA0, NextEA),
        instruction_get_dest_op(NextEA0,_,TmpOp),
        !op_regdirect_contains_reg(TmpOp,Reg)
    ),

    // ADD <Register> <Register> <Immediate>
    // e.g. add x0, x0, <OffsetOp>
    instruction_get_operation(NextEA, "ADD"),
    instruction_get_op(NextEA, 2, OffsetOp),
    op_immediate(OffsetOp, Offset),
    instruction_get_op(NextEA, 1, RegOp2),
    op_regdirect_contains_reg(RegOp2,Reg2),
    // Make sure the registers match
    Reg = Reg2.

/** E.g.,
adrp x0, L1
ldr x0, [x0,:lo12:L1]
*/
split_load(EA, NextEA, as(Base + Offset,address), "LDR/STR") :-
    // ADRP/ADR <Register> <Immediate>
    // e.g. adrp x0, BaseOp
    instruction_get_operation(EA,ADROperation),
    substr(ADROperation,0,3) = "ADR",
    instruction_get_op(EA, 1, BaseOp),
    op_immediate(BaseOp, Base),
    instruction_get_op(EA, 2, RegOp),
    op_regdirect_contains_reg(RegOp,Reg),

    // NOTE: Can't use def_used due to cyclic negation issue.
    //       This is a heuristic to look at next one or two instruction(s)
    //       to find LDR or STR instruction.
    (
        next(EA,NextEA)
        ;
        next(EA,NextEA0), next(NextEA0,NextEA),
        NextEA0 != NextEA,
        instruction_get_dest_op(NextEA0,_,TmpOp),
        !op_regdirect_contains_reg(TmpOp,Reg)
    ),

    // LDR/STR <Register> <IndirectOp>
    // e.g. ldr x0, [x0, <OffsetOp>]
    instruction_get_operation(NextEA, Operation),
    (
        arch.load_operation(Operation)
        ;
        arch.store_operation(Operation)
    ),
    instruction_get_op(NextEA, 1, OffsetOp),

    op_indirect_mapped(OffsetOp,"NONE",Reg,"NONE",_,Offset,_).

pc_relative_jump(EA, as(Dest,address)):-
    split_load(EA, _, Dest, "LDR/STR").

/**
A reference to a page-aligned value loaded by an adr or adrp instruction.

EA: Address of the adr/adrp instruction
Reg: The register that holds the used value
UsedEA: Address where the value is used
Dest: The destination address

We care primarily about adrp, but sometimes if the target is close enough,
the assembler will substitute an adr to the page-aligned address instead.

Reg is not necessarily the register loaded by adrp! It may be the result of:

    adrp x0, label
    mov x1, x0
UsedEA:
    # x1 is used

and Reg would be `x1`
*/
.decl adrp_used(EA:address,Reg:register,UsedEA:address,Dest:address)

adrp_used(EA,Reg,UsedEA,Dest):-
    const_value_reg_used(UsedEA,EA,EARegDef,Reg,as(Dest,number)),
    arch.adr_dest(EA,_,Dest),
    // Ensure that Dest is page-aligned - otherwise it's not a substituted
    // adrp, and can't be part of a split load.
    Dest band 0xFFF = 0,
    // The `add` of a split load may have an immediate of zero if the target
    // is page-aligned. If that address then has an addition offset added to
    // it, e.g.:
    //      adrp x8, .L_c4a000
    //      add x8,x8, :lo12:.L_c4a000
    //      add x8,x8,#8
    // then we will generate a false positive in the second `add`, since the
    // first is effectively a move. We only want to apply :lo12: to the first
    // "ADD" instruction.
    instruction_get_operation(EARegDef,Operation),
    Operation != "ADD".

/**
An instruction which could be the second half of a split load.
*/
.decl split_load_tail(EA:address,Offset:number,Reg:register,Type:symbol)

// ADD <Register> <Register> <Immediate>
// e.g. add x0, x0, <Offset>
split_load_tail(EA,Offset,Reg,Type):-
    arch.reg_arithmetic_operation(EA,_,Reg,1,Offset),
    !arch.memory_access(_,EA,_,_,_,_,_,_,_),
    Type = "ADD".

// LDR/STR <Register> <IndirectOp>
// e.g. ldr x0, [x0, <OffsetOp>]
split_load_tail(EA,Offset,Reg_nn,Type):-
    arch.memory_access(_,EA,_,_,_,Reg,"NONE",_,Offset),
    // The memory access cannot be a post-index operation. There should be no
    // immediate operand.
    instruction(EA,_,_,_,_,Op2,Op3,_,_,_),
    !op_immediate(Op2,_),
    !op_immediate(Op3,_),
    reg_nonnull(Reg,Reg_nn),
    Type = "LDR/STR".

/**
This version of split_load uses def_used, so this should not be used for
the code inference step due to cyclic negation issue.
Instead, this is for the symbolization step.
*/
.decl split_load_for_symbolization(ea:address, nextea:address, dest:address, type:symbol)

// Direct split load
split_load_for_symbolization(EA,UsedEA,as(as(Base,number)+Offset,address),Type):-
    adrp_used(EA,Reg,UsedEA,Base),
    split_load_tail(UsedEA,Offset,Reg,Type).

split_load_operand(EA, 1, as(Dest,address)) :-
    split_load_for_symbolization(EA, _, Dest, _).

split_load_operand(EA, 1, as(Dest,address)) :-
    split_load_for_symbolization(_, EA, Dest, "LDR/STR").

split_load_operand(EA, 2, as(Dest,address)) :-
    split_load_for_symbolization(_, EA, Dest, "ADD").


/**
ARM64: ex_false_pointer_array: optimized (e.g., -O2):

6e4:   adrp x0, .L_11018
6e8:   mov fp,sp
6ec:   stp x19,x20,[sp,#16]
6f0:   add x20,x0, :lo12:.L_11018
6f4:   mov x19,#0
6f8:   ldr x0,[x0,:lo12:.L_11018]

In optimized binary as above, there may be cases where one adrp instruction
is shared among multiple split_loads, such as the add and ldr:
(0x6e4,0x6f0), (0x6e4,06f8), respectively.
The relocation type of the object at .L_11018 is RELATIVE (global data
object).
In such case, using :got: for adrp and :got_lo12: for ldr causes runtime
failure. To avoid the problem, the relocation directives are not used for adrp and ldr.
*/
symbolic_operand_attribute(EA,1,"GOT"),
symbolic_operand_attribute(EA_ldr,1,"GOT"),
symbolic_operand_attribute(EA_ldr,1,"LO12"):-
    split_load_for_symbolization(EA,EA_ldr,Dest,"LDR/STR"),
    !split_load_for_symbolization(EA,EA_ldr,Dest,"ADD"),
    relocation(Dest,_,_,_,_,_,_),
    loaded_section(Beg,End,".got"),
    Beg <= Dest, Dest < End.

symbolic_operand_attribute(EA,1,"LO12"):-
    split_load_for_symbolization(EA0,EA,Dest,"LDR/STR"),
    (
        !relocation(Dest,_,_,_,_,_,_),
        UNUSED(EA0)
        ;
        split_load_for_symbolization(EA0,_,Dest,"ADD")
        ;
        loaded_section(Beg,End,".got"),
        (
            Beg > Dest, UNUSED(End);
            Dest >= End, UNUSED(Beg)
        )
    ).

symbolic_operand_attribute(EA,2,"LO12"):-
    split_load_for_symbolization(_,EA,Dest,"ADD"),
    !relocation(as(Dest,address),"GLOB_DAT",_,_,_,_,_).

value_reg(EA,Reg,EA,"NONE",0,Val,1):-
    split_load_for_symbolization(_,EA,RefAddr,_),
    arch.load(EA,OpIndex,_,Reg,_,_,_,_),
    track_register(Reg),
    instruction_get_op(EA,OpIndex,Op1),
    op_indirect(Op1,_,_,_,_,_,DataSize),
    data_word(RefAddr,DataSize/8,Val).

symbolic_operand_candidate(EA,1,Dest,Type):-
    // ADR <Register> <Immediate>
    // e.g. adr r1, BaseOp
    code(EA),
    instruction_get_operation(EA, Operation),
    (
        Operation = "ADR";
        Operation = "ADRP"
    ),
    !split_load_operand(EA,1,_),
    instruction_get_op(EA, 1, OffsetOp),
    op_immediate(OffsetOp, Offset),
    Dest = as(Offset,address),
    (
        code(Dest), Type="code"
        ;
        data_segment(Begin,End),
        Dest >= Begin, Dest <= End,
        Type = "data"
    ).

unlikely_have_symbolic_immediate(EA):-
    instruction_get_operation(EA, Operation),
    contains("ADD", Operation),
    !split_load(_,EA,_,"ADD").
